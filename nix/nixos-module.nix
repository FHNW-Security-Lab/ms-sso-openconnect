{ config, lib, pkgs, ... }:

let
  cfg = config.services.ms-sso-openconnect;
  resolvconfBin = lib.getExe' pkgs.openresolv "resolvconf";
  resolvectlBin = lib.getExe' pkgs.systemd "resolvectl";
  grepBin = lib.getExe' pkgs.gnugrep "grep";
  awkBin = lib.getExe' pkgs.gawk "awk";
  cleanupScript = pkgs.writeShellScript "nm-ms-sso-cleanup" ''
    iface="''${VPN_IP_IFACE:-''${DEVICE_IFACE:-$1}}"
    event="$2"

    case "$event" in
      vpn-down|down)
        if [ -r /etc/resolv.conf ] && ${grepBin} -q '^# Generated by resolvconf' /etc/resolv.conf; then
          if [ -n "$iface" ]; then
            ${resolvconfBin} -d "$iface" || true
          fi
          ${resolvconfBin} -l | ${awkBin} '/^# resolv.conf from /{print $5}' | ${grepBin} '^tun' | while read -r dev; do
            [ -n "$dev" ] && ${resolvconfBin} -d "$dev" || true
          done
          ${resolvconfBin} -u || true
        elif [ -n "$iface" ] && [ -x ${resolvectlBin} ]; then
          ${resolvectlBin} revert "$iface" || true
        fi
        ;;
    esac
  '';
in
{
  options.services.ms-sso-openconnect = {
    enable = lib.mkEnableOption "MS SSO OpenConnect NetworkManager plugin";

    withUi = lib.mkOption {
      type = lib.types.bool;
      default = false;
      description = "Install the Qt UI package alongside the NetworkManager plugin.";
    };

    withOverlay = lib.mkOption {
      type = lib.types.bool;
      default = true;
      description = "Add the local overlay that provides the ms-sso-openconnect packages.";
    };

    autoKillStale = lib.mkOption {
      type = lib.types.bool;
      default = true;
      description = "Kill stale nm-ms-sso-service processes on NetworkManager restart.";
    };

    autoCleanupDns = lib.mkOption {
      type = lib.types.bool;
      default = true;
      description = "Clean up DNS entries on VPN disconnect.";
    };
  };

  config = lib.mkIf cfg.enable {
    nixpkgs.overlays = lib.optionals cfg.withOverlay [
      (import ./overlay.nix)
    ];

    networking.networkmanager.plugins = lib.mkAfter [
      pkgs.networkmanager-ms-sso
    ];

    environment.systemPackages = lib.optionals cfg.withUi [
      pkgs.ms-sso-openconnect-ui
    ];

    systemd.services.NetworkManager.serviceConfig.ExecStartPre =
      lib.optional cfg.autoKillStale
        "-${lib.getExe' pkgs.procps "pkill"} -KILL -f nm-ms-sso-service";

    networking.networkmanager.dispatcherScripts = lib.optional cfg.autoCleanupDns {
      source = cleanupScript;
      type = "basic";
    };
  };
}
